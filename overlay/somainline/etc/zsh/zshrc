# Keybinds
###########

# Fix Home, End & Delete
bindkey '^[[H' beginning-of-line
bindkey '^[[F' end-of-line
bindkey '^[[3~' delete-char

# Ctrl + Arrows
bindkey '^[[1;5D' backward-word
bindkey '^[[1;5C' forward-word

# Ctrl + (Backspace/Delete)
bindkey '^H' backward-kill-word
bindkey '^[[3;5~' kill-word

# Shift + Tab reverse completion
bindkey '^[[Z' reverse-menu-complete

# Ctrl + R history search
bindkey '^R' history-incremental-search-backward

# History
##########
HISTFILE="$HOME/.cache/zsh_history"
HISTSIZE=4096
SAVEHIST=4096
setopt histignorealldups # ignore ALL duplicated commands
setopt hist_ignore_space # don't save commands starting with a space in history

# Aliases
##########
alias \
	ip='ip -c' \
	diff='diff --color' \
	grep='grep --color' \
	egrep='grep -E' \
	devmem='sudo busybox devmem' \
	'..'='cd ..'

# Misc
#######

# for some reason "man" defaults to "more" under zsh by default
export MANPAGER=less

# Don't consider any special character part of a word. Allows ctrl+w'ing
# up to the previous `/` separator, for example. See also:
# https://github.com/ohmyzsh/ohmyzsh/issues/8743
# https://github.com/ohmyzsh/ohmyzsh/commit/50dc4ab3574f4e265dff816d8d9a0195cd260152
WORDCHARS=''

# make e.g. 'gsettings' work over SSH
if [ -z "$DISPLAY" ] && (pgrep -x Xwayland || pgrep -x Xorg) >/dev/null; then
	export DISPLAY=:0
fi

# Prompt
#########
[ $EUID -eq 0 ] && PROMPT_COLOR='red' || PROMPT_COLOR='cyan'
PROMPT="%F{$PROMPT_COLOR}%m%f %F{yellow}%1~%f %(!.#.$) "

# Completions + suggestions
############################
[ -e /usr/share/zsh/site-functions ] && fpath=(/usr/share/zsh/site-functions $fpath)

autoload -Uz compinit
compinit
zstyle ':completion:*' menu select
# pick up new binaries from PATH automagically after shell startup
zstyle ':completion:*' rehash true
